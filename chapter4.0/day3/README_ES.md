# Cap√≠tulo 4 D√≠a 3 - Creando un Contrato de NFT: Colecciones (Parte 1/3)

Aprendiste mucho hasta ahora. Apliquemos todo lo que has aprendido para hacer tu propio contrato de NFT. 

## Video

En los pr√≥ximos cap√≠tulos, vamos a hacer exactamente lo que hago en este video. Hoy, vamos solamente desde 00:00 - 20:35: https://www.youtube.com/watch?v=bQVXSpg6GE8 

## Reviso

<img src="../images/accountstorage1.PNG" />
<img src="../images/capabilities.PNG" />

## Ejemplo de NFT(NonFungibleToken)

Gastemos los siguientes d√≠as trabajando a trav√©s de un ejemplo de NonFungibleToken. Vamos a crear nuestro propio contrato de NFT llamado CryptoPoops. As√≠ que vas a revisar todo los conceptos que has aprendido hasta ahora, e implementar tu propio NFT!

Empecemos haciendo un contrato: 

```cadence
pub contract CryptoPoops {
  pub var totalSupply: UInt64

  pub resource NFT {
    pub let id: UInt64

    init() {
      // NOTA: Cada recurso en Flow tiene su propio `uuid`. Nunca
      // habr√° recursos con lo mismo `uuid`.
      self.id = self.uuid
    }
  }

  pub fun createNFT(): @NFT {
    return <- create NFT()
  }

  init() {
    self.totalSupply = 0
  }
}
```

Empezamos por:
1. Definido un `totalSupply` (poniendo igual a 0)
2. Creando un tipo de `NFT`. Damos el `NFT` 1 campo: `id`. El `id` es igual a `self.uuid`, cual es un identificador √∫nico que cada recurso en Flow tiene. Nunca habr√° dos recursos con la misma `uuid`, as√≠ que funciona perfectamente como un id para un NFT, puesto que un NFT es un token que es √∫nico completamente de cada otra token. 
3. Creando un funcion de `createNFT` que devuelve un recurso de `NFT`, as√≠ que cualquier persona puede crear su propia NFT. 

Bueno, eso es f√°cil. Guardamos un NFT en el almacenamiento de nuestra cuenta, y lo ponemos a disposici√≥n al p√∫blico. 

```cadence
import CryptoPoops from 0x01
transaction() {
  prepare(signer: AuthAccount) {
    // Guarda un NFT al camino de almacenamiento de `/storage/MyNFT`
    signer.save(<- CryptoPoops.createNFT(), to: /storage/MyNFT)
    
    // Conecta a publico asi que cualquier persona puede leer el campo de `id` de mi NFT
    signer.link<&CryptoPoops.NFT>(/public/MyNFT, target: /storage/MyNFT)
  }
}
```

¬°Genial! Debes entender esto ahora porque de la √∫ltimo cap√≠tulo. Primero guardamos el NFT en el almacenamiento de la cuenta, y conecta una referencia al p√∫blico as√≠ que podemos leer los campos de `id` con un gui√≥n. Bueno, hagamos eso!

```cadence
import CryptoPoops from 0x01
pub fun main(address: Address): UInt64 {
  let nft = getAccount(address).getCapability(/public/MyNFT)
              .borrow<&CryptoPoops.NFT>()
              ?? panic("An NFT does not exist here.")
  
  return nft.id // 3525 (alg√∫n n√∫mero al azar, porque es el `uuid`
                // del recurso. Esto probablemente va a ser diferente para ti.)
}
```

¬°Genial! Hicimos algunas cosas buenas. Pero pensemos sobre esto por un momento. ¬øQu√© pasa si queremos guardar *otra* NFT en nuestra cuenta?

```cadence
import CryptoPoops from 0x01
transaction() {
  prepare(signer: AuthAccount) {
    // ERROR: "failed to save object: path /storage/MyNFT 
    // in account 0x1 already stores an object"
    signer.save(<- CryptoPoops.createNFT(), to: /storage/MyNFT)
    
    signer.link<&CryptoPoops.NFT>(/public/MyNFT, target: /storage/MyNFT)
  }
}
``` 

Mira que pasa. ¬°Conseguimos un error! ¬øPor qu√©? Porque ya existe un NFT en ese camino de almacenamiento. ¬øC√≥mo podemos resolver esto? Pues, podemos especificar un camino diferente de almacenamiento‚Ä¶

```cadence
import CryptoPoops from 0x01
transaction() {
  prepare(signer: AuthAccount) {
    // Nota que usamos `MyNFT02` como el camino
    signer.save(<- CryptoPoops.createNFT(), to: /storage/MyNFT02)
    
    signer.link<&CryptoPoops.NFT>(/public/MyNFT02, target: /storage/MyNFT02)
  }
}
```

Esta manera funciona, pero no es buena. Si queremos tener un mont√≥n de NFTs, tenemos que recordar todos los caminos de almacenamiento, y eso es super molestando e ineficiente.

El segundo problema es que nadie puede darnos NFTs. Ya que solo el propietario de la cuenta puede guardar un NFT en el almacenamiento de su cuenta directamente, nadie puede crear un NFT para nosotros. Eso tampoco es bueno. 

## Colecciones

La manera para resolver ambas de esos problemas es crear una ‚ÄúColecci√≥n‚Äù o un contenedor que envuelve todas nuestras NFTs en uno.  Entonces, podemos guardar la colecci√≥n a 1 camino de almacenamiento, y tambi√©n permite otras para ‚Äúdepositar‚Äù en esa colecci√≥n. 

```cadence
pub contract CryptoPoops {
  pub var totalSupply: UInt64

  pub resource NFT {
    pub let id: UInt64

    init() {
      self.id = self.uuid
    }
  }

  pub fun createNFT(): @NFT {
    return <- create NFT()
  }

  pub resource Collection {
    // Mapas un `id` a el NFT con eso `id`
    //
    // Ejemplo: 2353 => NFT con id 2353
    pub var ownedNFTs: @{UInt64: NFT}

    // Nos permite a depositar un NFT
    // a nuestro Colecci√≥n
    pub fun deposit(token: @NFT) {
      self.ownedNFTs[token.id] <-! token
    }

    // Nos permite a retirar un NFT
    // de nuestro Colecci√≥n 
    //
    //  Si el NFT no existe, p√°nico
    pub fun withdraw(withdrawID: UInt64): @NFT {
      let nft <- self.ownedNFTs.remove(key: withdrawID) 
              ?? panic("Este NFT no existen en este Colecci√≥n.")
      return <- nft
    }

    // Devuelve un array de todo los ids de los NFTs en nuestra Colecci√≥n.
    pub fun getIDs(): [UInt64] {
      return self.ownedNFTs.keys
    }

    init() {
      self.ownedNFTs <- {}
    }

    destroy() {
      destroy self.ownedNFTs
    }
  }

  pub fun createEmptyCollection(): @Collection {
    return <- create Collection()
  }

  init() {
    self.totalSupply = 0
  }
}
```

Genial. Definimos un recurso de `Collection` que hace algunas cosas:
1. Guarda un diccionario llamado `ownedNFTs` que mapa un `id` a el `NFT` con eso `id`. 
2. Define una funci√≥n de `deposit` para depositar `NFTs`.
3. Define una funci√≥n de `withdraw` para retirar `NFTs`.
4. Define una funci√≥n de `getIDs` as√≠ que podemos conseguir una lista de todo los `id` de los `NFT` en nuestra Colecci√≥n. 
5. Define una funci√≥n de `destroy`. En Cadence, **cuando tiene recursos dentro recursos, TIENES que declarar una funci√≥n de `destroy` que destruye manualmente esos recursos con la palabra clave `destroy`.**

Tambi√©n hemos definido una funci√≥n de `createEmptyCollection` as√≠ que podemos guardar una `Collection` a el almacenamiento de nuestra cuenta as√≠ que podemos manejar nuestros NFTs mejor. Hagamos esto ahora: 

```cadence
import CryptoPoops from 0x01
transaction() {
  prepare(signer: AuthAccount) {
    // Guarda un `CryptoPoops.Collection` en el almacenamiento de nuestra cuenta
    signer.save(<- CryptoPoops.createEmptyCollection(), to: /storage/MyCollection)
    
    // Conecta al p√∫blico 
    signer.link<&CryptoPoops.Collection>(/public/MyCollection, target: /storage/MyCollection)
  }
}
```

Toma unos momentos para leer este c√≥digo. ¬øQu√© hay de malo en esto? Pensar sobre algunos de los problemas de seguridad que tiene. ¬øPor qu√© es malo que expongamos `&CryptoPoops.Collection` al p√∫blico?

....

....

¬øHas pensado en ello todav√≠a? La raz√≥n es porque ahora, **¬°cualquier persona puede retirar de nuestra Colecci√≥n!** Eso es muy malo. 

El problema, aunque, es qu√© queremos el p√∫blico poder `deposit` NFTs en nuestra Colecci√≥n, y queramos a tambi√©n poder leer los ids de los NFTs que poseemos. 

Las interfaces de recursos, ¬°woo! Definamos una interfaz de recurso para restring que exponemos al p√∫blico: 

```cadence
pub contract CryptoPoops {
  pub var totalSupply: UInt64

  pub resource NFT {
    pub let id: UInt64

    init() {
      self.id = self.uuid
    }
  }

  pub fun createNFT(): @NFT {
    return <- create NFT()
  }

  // Solo expone `deposit` y `getIDs`
  pub resource interface CollectionPublic {
    pub fun deposit(token: @NFT)
    pub fun getIDs(): [UInt64]
  }

  // `Collection` implementa `CollectionPublic` ahora
  pub resource Collection: CollectionPublic {
    pub var ownedNFTs: @{UInt64: NFT}

    pub fun deposit(token: @NFT) {
      self.ownedNFTs[token.id] <-! token
    }

    pub fun withdraw(withdrawID: UInt64): @NFT {
      let nft <- self.ownedNFTs.remove(key: withdrawID) 
              ?? panic("Este NFT no existen en este Colecci√≥n.")
      return <- nft
    }

    pub fun getIDs(): [UInt64] {
      return self.ownedNFTs.keys
    }

    init() {
      self.ownedNFTs <- {}
    }

    destroy() {
      destroy self.ownedNFTs
    }
  }

  pub fun createEmptyCollection(): @Collection {
    return <- create Collection()
  }

  init() {
    self.totalSupply = 0
  }
}
```

Ahora podemos restringir que el p√∫blico puedes ver cuando guardamos nuestra Colecci√≥n a el almacenamiento de nuestra cuenta: 

```cadence
import CryptoPoops from 0x01
transaction() {
  prepare(signer: AuthAccount) {
    // Store a `CryptoPoops.Collection` in our account storage.
    signer.save(<- CryptoPoops.createEmptyCollection(), to: /storage/MyCollection)
    
    // NOTA: Exponemos `&CryptoPoops.Collection{CryptoPoops.CollectionPublic}`, cu√°l  
    // solo continente `deposit` y `getIDs`.
    signer.link<&CryptoPoops.Collection{CryptoPoops.CollectionPublic}>(/public/MyCollection, target: /storage/MyCollection)
  }
}
```

<img src="../images/thanos.png" />
Ahora esto‚Ä¶me hace feliz. Experimentemos depositando un NFT en nuestra cuenta y retirarlo. 

```cadence
import CryptoPoops from 0x01
transaction() {
  prepare(signer: AuthAccount) {
    // Consigue una referencia a nuestra `CryptoPoops.Collection`
    let collection = signer.borrow<&CryptoPoops.Collection>(from: /storage/MyCollection)
                      ?? panic("Este recipiente no tiene una Colecci√≥n.‚Äù)
    
    // Deposita un `NFT` a nuestra Colecci√≥n
    collection.deposit(token: <- CryptoPoops.createNFT())

    log(collection.getIDs()) // [2353]

    // Retirar el `NFT` de nuestro Colecci√≥n
    let nft <- collection.withdraw(withdrawID: 2353) // Conseguimos este n√∫mero de el array de los ids arriba
  
    log(collection.getIDs()) // []

    destroy nft
  }
}
```

¬°Genial! As√≠ que todo est√° funcionando bien. Ahora veamos si alguien puede depositar a NUESTRA Colecci√≥n en lugar de hacerlo nosotros mismos. 

```cadence
import CryptoPoops from 0x01
transaction(recipient: Address) {

  prepare(otherPerson: AuthAccount) {
    // Consigue una referencia a el Colecci√≥n p√∫blico de el `recipient`.
    let recipientsCollection = getAccount(recipient).getCapability(/public/MyCollection)
                                  .borrow<&CryptoPoops.Collection{CryptoPoops.CollectionPublic}>()
                                  ?? panic("Este recipiente no tiene un Colecci√≥n.")
    
    // Deposita un `NFT` a nuestra Colecci√≥n 
    recipientsCollection.deposit(token: <- CryptoPoops.createNFT())
  }

}
```

Buennnoo. Lo depositamos en la cuenta de alguien mas, cual es posible porque ellos connectaron `&CryptoPoops.Collection{CryptoPoops.CollectionPublic}` al publico. Y eso est√° bien. No importa si damos un NFT a alguien gratis. ¬°Eso es genial!

Ahora, ¬øqu√© pasa si intentamos a retirar de la Colecci√≥n de alguien? 

```cadence
import CryptoPoops from 0x01
transaction(recipient: Address, withdrawID: UInt64) {

  prepare(otherPerson: AuthAccount) {
    // Consigue una referencia a la Coleccion publico de el `recipient`
    let recipientsCollection = getAccount(recipient).getCapability(/public/MyCollection)
                                  .borrow<&CryptoPoops.Collection{CryptoPoops.CollectionPublic}>()
                                  ?? panic("Este recipiente no tiene una Colecci√≥n.")
    
    // ERROR: "Member of restricted type is not accessible: withdraw"
    recipientsCollection.withdraw(withdrawID: withdrawID)
  }

}
```

¬°Conseguimos un error! Perfecto, ahora el hacker no puede robar nuestras NFTs :)

Por √∫ltimo, intentemos a leer los NFTs en nuestra cuenta usando un gui√≥n: 

```cadence
import CryptoPoops from 0x01
pub fun main(address: Address): [UInt64] {
  let publicCollection = getAccount(address).getCapability(/public/MyCollection)
              .borrow<&CryptoPoops.Collection{CryptoPoops.CollectionPublic}>()
              ?? panic("La direcci√≥n no tienes una Colecci√≥n.")
  
  return publicCollection.getIDs() // [2353]
}
```

Bueno, listo.

## En Conclusi√≥n

Colecciones no solo por NFTs. Vas a ver el concepto de una Colecci√≥n usado en toddddaaaasss paaarrrteeess de el ecosistema de Flow. Si quieres los usadores guarda un recurso, pero ellos tienen m√∫ltiple de ese recurso, siempre vas a usar una Colecci√≥n para envolver alrededor de ellos, as√≠ que puedes guardar todos en un lugar. Es un concepto muy importante a entender. 

Y con eso, date un aplauso. ¬°Te implementa un contrato de NFT funcionando! ¬°Est√°s volviendo bueno! Diablos, puedes alcanzarme pronto. Es broma, eso no es posible. Soy mucho mejor que t√∫.

## B√∫squedas

1. ¬øPor qu√© a√±adimos una Colecci√≥n a este contrato? Enumere las dos razones principales. 

2. ¬øQu√© tienes que hacer si tienes un recurso(s) dentro de otro recurso?

3. Pensar algunas cosas adicionales que podemos a√±adir a este contrato. Pensar sobre que puede ser problem√°tica con este contrato y como podemos resolverlo. 

    -  Idea #1: ¬øRealmente queremos que todos puedan crear un NFT? ü§î

    - Idea #2: Si queremos leer informaci√≥n sobre nuestras NFTs dentro nuestra Colecci√≥n, ahora tenemos que retirarlo de la Colecci√≥n para hacerlo. Es este buena? 
